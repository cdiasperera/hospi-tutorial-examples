load chanset

fmod APISYNTAX-SESSION is
   inc CINNI .
   inc CHANSET .

   ***********************************************************
   ***      Syntax
   ***********************************************************
   sorts Trm Guard Qualifier .
   sort Choice Choiceset .
   subsort Choice < Choiceset .

   ops lin un : -> Qualifier [ctor] .
   ops True False : -> Value [ctor] .
   op  __(_) : Qualifier Chan Qid -> Guard [ctor prec 5] .
   op _<_> : Chan Value -> Guard [ctor prec 6] .

   op nil : -> Trm [ctor] .
   op new[__]_ : Qid Qid Trm -> Trm [ctor prec 10] .
   op _|_  :  Trm Trm -> Trm [ctor assoc comm prec 12 id: nil] .
   op if_then_else_fi : Value Trm Trm -> Trm [ctor prec 8] .
   op _ << _ . _ : Chan Qid Trm -> Trm [ctor prec 15] .
   op _ >> {_} : Chan Choiceset -> Trm [ctor prec 17] .
   op _._ : Guard Trm -> Trm [ctor prec 7] .

   op _:_ : Qid Trm -> Choice . 
   op empty : -> Choiceset [ctor] .
   op __ : Choiceset Choiceset -> Choiceset [ctor assoc comm id: empty] .
   op _in_  : Choice Choiceset -> Bool .
   op _subset_ : Choiceset Choiceset -> Bool .

   var S : Subst .
   vars a b : Chan .
   vars P Q : Trm .
   vars x y : Qid .
   var v v1 v2 : Value .
   var q : Qualifier .
   var CH : Choiceset .

   var s : String .
   var n : Nat .

   ***********************************************************
   ***         Substitution rules
   ***********************************************************
   op  __ :  Subst Trm -> Trm [prec 3] .
   op  subst-aux : Subst Choiceset -> Choiceset .
   eq  S nil = nil .
   eq  S (new [x y] P) = new [x y] ([lift x S] [lift y S] P) .
   eq  S (q a(y) . P ) = q (S a)(y) . ([lift y S] P) .
   eq  S (a < b > . P) = (S a) < (S b) > . (S P) .
   ceq S (a < v > . P) = (S a) < v > . (S P) if v == True or v == False . 
   ceq S (if v then P else Q fi) = if v then (S P) else (S Q) fi if v == True or v == False .
   eq  S (if val(x) then P else Q fi) = if (S val(x)) then (S P) else (S Q) fi .
   eq  S (a >> {CH}) = (S a) >> { subst-aux(S, CH) } .
   eq  S (a << x . P) = (S a) << x . (S P) .
   eq  S (P | Q) = (S P) | (S Q) .
   eq  subst-aux(S, empty) = empty .
   eq  subst-aux(S, (x : P) CH) = (x : (S P)) subst-aux(S, CH) .

   
   ***********************************************************
   ***          Free names in a term
   ***********************************************************
   op  freevars : Trm -> Chanset .
   op  freevars-aux : Choiceset -> Chanset .
   eq  freevars(nil) = mt .
   eq  freevars(new [x y] P) = down(x, down(y, freevars(P))) .
   eq  freevars(q a(y) . P) = a down(y, freevars(P)) .
   ceq freevars(P | Q) = freevars(P) freevars(Q) if P =/= nil and Q =/= nil .
   eq  freevars(a < b > . P) = a b freevars(P) .
   ceq freevars(a < v > . P) = a freevars(P) if v == True or v == False .
   ceq freevars(if v then P else Q fi) = freevars(P) freevars(Q) if v == True or v == False .
   eq  freevars(if val(x) then P else Q fi) = freevars(P) freevars(Q) .
   eq  freevars(a >> {CH}) = a freevars-aux(CH) .
   eq  freevars(a << x . P) = a freevars(P) .
   eq  freevars-aux(empty) = mt .
   eq  freevars-aux((x : P) CH) = freevars(P) freevars-aux(CH) .
endfm